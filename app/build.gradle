plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id "org.jetbrains.kotlin.kapt"
}

def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def sdk = 33

android {
    compileSdk sdk
    buildToolsVersion '33.0.2'
    ndkVersion '25.2.9519653'

    defaultConfig {
        applicationId 'gm.tieba.tabswitch'
        minSdk 24
        targetSdk sdk
        versionCode gitCommitCount
        versionName '2.8.6-beta'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
                arguments '-DANDROID_STL=none'
            }
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "TS_${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/**', '/kotlin/**', '/okhttp3/**']
        }
        jniLibs {
            excludes += ['**/liblog.so', '/lib/x86/**', '/lib/x86_64/**']
        }
    }
    buildFeatures {
        prefab true
    }
    lint {
        checkDependencies true
    }
    namespace 'gm.tieba.tabswitch'
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    api 'androidx.annotation:annotation:1.6.0'
    def room_version = '2.5.1'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'io.reactivex.rxjava3:rxjava:3.1.2'
    implementation 'org.luckypray:DexKit:1.1.8'
    // https://stackoverflow.com/questions/61195038/excluding-dependency-from-implementation-files
    implementation files('apktool_2.7.0.jar')
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'
}
