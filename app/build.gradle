plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def latestTag = String.valueOf('git describe --abbrev=0 --tags'.execute([], project.rootDir).text.trim())
def commitCountSinceLatestTag = String.valueOf(('git rev-list --count ' + latestTag + '..HEAD').execute([], project.rootDir).text.trim())
def sdk = 34

android {
    compileSdk sdk
    buildToolsVersion = '34.0.0'
    ndkVersion '26.0.10792818'

    defaultConfig {
        applicationId 'gm.tieba.tabswitch'
        minSdk 28
        targetSdk sdk
        versionCode gitCommitCount
        versionName '3.0.0-alpha'
        if (versionName.contains('alpha') || versionName.contains('beta')) {
            versionNameSuffix = '.' + commitCountSinceLatestTag
        }
        buildConfigField "String", "TARGET_VERSION", "\"12.59.1.0\""
        buildConfigField "String", "MIN_VERSION", "\"12.53.1.0\""

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                abiFilters 'arm64-v8a'
                arguments '-DANDROID_STL=none'
            }
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "TS_${defaultConfig.versionName}${defaultConfig.versionNameSuffix ?: ''}_${variant.buildType.name}.apk"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/**', '/kotlin/**', '/okhttp3/**']
        }
        jniLibs {
            excludes += ['**/liblog.so', '/lib/x86/**', '/lib/x86_64/**']
        }
    }
    buildFeatures {
        prefab true
        buildConfig true
    }
    lint {
        checkDependencies true
    }
    namespace 'gm.tieba.tabswitch'
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    api 'androidx.annotation:annotation:1.7.1'
    def roomVersion = '2.6.1'
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"

    implementation 'io.reactivex.rxjava3:rxjava:3.1.8'
    implementation 'org.luckypray:dexkit:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'
}
